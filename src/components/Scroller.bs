sub init()
    m.clipArea = m.top.findNode("clipArea")
    m.animation = m.top.findNode("scrollAnimation")
    m.interpolator = m.top.findNode("scrollInterpolator")
    m.scrollTarget = invalid
end sub

sub setContent(node as object, scrollable as boolean)
    if m.scrollTarget <> invalid
        m.clipArea.removeChild(m.scrollTarget)
        m.scrollTarget = invalid
    end if
    m.scrollTarget = node
    m.scrollTarget.id = "scrollTarget"

    width = m.top.width
    height = m.top.height

    m.clipArea.clippingRect = [0, 0, width, height]
    m.clipArea.appendChild(m.scrollTarget)

    m.scrollable = scrollable
    m.scrollTop = 0
    m.animatedScrolling = m.top.animatedScrolling

    if scrollable
        m.maxScroll = m.scrollTarget.boundingRect().height - height
        if m.maxScroll < 0 then m.maxScroll = 0
        m.scrollStep = m.top.scrollStep
        if m.scrollStep = 0 then m.scrollStep = int(height / 3)
        m.top.scrollRatio = height / (height + m.maxScroll)
    else
        m.maxScroll = 0
        m.scrollStep = 0
        m.top.scrollRatio = 1
    end if
    m.top.scrollFraction = 0
end sub

function scrollBy(distance as integer) as boolean
    if m.maxScroll = 0 then return false

    ' calculate target position
    prevTop = m.scrollTop
    target = prevTop + distance
    if target < 20 ' snap to boundaries
        target = 0
    else if target > m.maxScroll - 20
        target = m.maxScroll
    end if
    if target = prevTop then return false
    m.scrollTop = target

    ' update translation
    if m.animatedScrolling
        m.animation.control = "stop"
        m.interpolator.keyValue = [
            m.scrollTarget.translation,
            [0, -target]
        ]
        m.animation.control = "start"
    else
        m.scrollTarget.translation = [0, -target]
    end if

    m.top.scrollFraction = target / m.maxScroll
    return true
end function

function onKeyEvent(key as string, press as boolean) as boolean
    if not press or not m.scrollable then return false
    if key = "down"
        return scrollBy(m.scrollStep)
    else if key = "up"
        return scrollBy(-m.scrollStep)
    end if
    return false
end function
